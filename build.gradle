apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'idea'

mainClassName = 'io.pivotal.platformMonitoring.kpiValidator.Validator'

jar {
    baseName = 'platform-monitoring-validator-1.1-jar-with-dependencies'
    manifest {
        attributes 'Main-Class': 'io.pivotal.platformMonitoring.kpiValidator.Validator'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url 'http://repo.spring.io/snapshot' }
    mavenCentral()
}

dependencies {
    compile(
     'org.cloudfoundry:cloudfoundry-client-reactor:2.9.0.RELEASE',
     'org.cloudfoundry:cloudfoundry-operations:2.9.0.RELEASE',
     'io.projectreactor:reactor-core:3.0.6.RELEASE',
     'io.projectreactor.ipc:reactor-netty:0.6.2.RELEASE',
     'org.slf4j:slf4j-log4j12:1.7.25',
     'org.slf4j:slf4j-api:1.7.25',
     'com.jamonapi:jamon:2.81'
    )



    testCompile(
        'org.assertj:assertj-core:3.6.2',
        'org.springframework.boot:spring-boot-starter-test:1.5.2.RELEASE',
        'org.powermock:powermock-mockito-release-full:1.6.4',
        'org.awaitility:awaitility:2.0.0',
        'junit:junit:4.12',
        'com.github.stefanbirkner:system-rules:1.16.1'
    )

}
String ANSI_RESET = "\u001B[0m";
String ANSI_RED = "\u001B[31m";
String ANSI_GREEN = "\u001B[32m";
String ANSI_YELLOW = "\u001B[33m";
boolean verboseTests = System.properties['verboseTests'] != null

test {
    afterTest { desc, result ->
        String color

        switch (result.resultType) {
        case TestResult.ResultType.SUCCESS:
            color = ANSI_GREEN
            break
        case TestResult.ResultType.FAILURE:
            color = ANSI_RED
            break
        case TestResult.ResultType.SKIPPED:
            color = ANSI_YELLOW
            break
        default:
            color = ANSI_RESET
            break
        }

        println "${color}${result.resultType} $ANSI_RESET: ${desc.name} (${desc.className})"
    }

    testLogging {
            exceptionFormat 'full'
            showCauses true
            showExceptions true
            showStackTraces verboseTests     // switch me on with -D verboseTests
            showStandardStreams verboseTests // switch me on with -D verboseTests
    }
}
